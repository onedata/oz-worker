%%%--------------------------------------------------------------------
%%% @author Michal Zmuda
%%% @copyright (C) 2016 ACK CYFRONET AGH
%%% This software is released under the MIT license
%%% cited in 'LICENSE.txt'.
%%% @end
%%%--------------------------------------------------------------------
%%% @doc Subscriptions WSS listener
%%% @end
%%%--------------------------------------------------------------------
-module(subscriptions_wss_listener).
-author("Michal Zmuda").

-include("rest_config.hrl").
-include("registered_names.hrl").
-include_lib("ctool/include/logging.hrl").

-behaviour(listener_behaviour).

%% listener_behaviour callbacks
-export([port/0, start/0, stop/0, healthcheck/0]).

-define(WSS_LISTENER, subscriptions_wss).

%%%===================================================================
%%% listener_starter_behaviour callbacks
%%%===================================================================

%%--------------------------------------------------------------------
%% @doc
%% {@link listener_starter_behaviour} callback port/0.
%% @end
%%--------------------------------------------------------------------
-spec port() -> integer().
port() ->
    {ok, WssPort} = application:get_env(?APP_Name, subscriptions_wss_port),
    WssPort.


%%--------------------------------------------------------------------
%% @doc
%% {@link listener_starter_behaviour} callback start/1.
%% @end
%%--------------------------------------------------------------------
-spec start() -> ok | {error, Reason :: term()}.
start() ->
    try
        % Get rest config
        Port = port(),
        {ok, HttpsAcceptors} = application:get_env(?APP_Name, subscroptions_https_acceptors),

        % Get cert paths
        {ok, ZoneCADir} = application:get_env(?APP_Name, zone_ca_dir),
        {ok, GuiCertFile} = application:get_env(?APP_Name, gui_cert_file),
        {ok, GuiKeyFile} = application:get_env(?APP_Name, gui_key_file),
        {ok, GuiCaCertFile} = application:get_env(?APP_Name, gui_cacert_file),

        {ok, ZoneCABin} = file:read_file(zone_ca:cacert_path(ZoneCADir)),
        [{_, ZoneCADER, _} | _] = public_key:pem_decode(ZoneCABin),

        {ok, GuiCABin} = file:read_file(GuiCaCertFile),
        [{_, GuiCADER, _} | _] = public_key:pem_decode(GuiCABin),

        Dispatch = [
            {'_', [
                {"/subscriptions/[...]", subscriptions_wss_handler, []}
            ]}
        ],


        {ok, _} = cowboy:start_https(?WSS_LISTENER, HttpsAcceptors,
            [
                {port, Port},
                % @todo Use gui cert files rather than certs generated by GR, since
                % we don't yet have a mechanism of distributing the CA cert.
                {certfile, GuiCertFile},
                {keyfile, GuiKeyFile},
                {cacerts, [GuiCADER, ZoneCADER]},
                {verify, verify_peer},
                {ciphers, ssl:cipher_suites() -- weak_ciphers()},
                {versions, ['tlsv1.2', 'tlsv1.1']}
            ],
            [
                {env, [{dispatch, cowboy_router:compile(Dispatch)}]}
            ]),
        ok
    catch
        _Type:Error ->
            ?error_stacktrace("Could not start wss, error: ~p", [Error]),
            {error, Error}
    end.


%%--------------------------------------------------------------------
%% @doc
%% {@link listener_starter_behaviour} callback stop/1.
%% @end
%%--------------------------------------------------------------------
-spec stop() -> ok | {error, Reason :: term()}.
stop() ->
    case catch cowboy:stop_listener(?WSS_LISTENER) of
        (ok) ->
            ok;
        (Error) ->
            ?error("Error on stopping listener ~p: ~p", [?WSS_LISTENER, Error]),
            {error, subscriptions_wss_stop_error}
    end.


%%--------------------------------------------------------------------
%% @doc
%% Returns the status of a listener.
%% @end
%%--------------------------------------------------------------------
-spec healthcheck() -> ok | {error, server_not_responding}.
healthcheck() ->
    Port = port(),
    case http_client:get("https://127.0.0.1:" ++ integer_to_list(Port), [], <<>>, [insecure]) of
        {ok, _, _, _} ->
            ok;
        _ ->
            {error, server_not_responding}
    end.

%% ====================================================================
%% @private
%% @doc
%% Returns list of weak ciphers.
%% @end
-spec weak_ciphers() -> list().
%% ====================================================================
weak_ciphers() ->
    [{dhe_rsa, des_cbc, sha}, {rsa, des_cbc, sha}].
